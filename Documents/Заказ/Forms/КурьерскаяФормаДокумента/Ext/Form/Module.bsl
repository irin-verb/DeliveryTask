
&НаКлиенте
Процедура НеБеру(Команда)
	ОтклонитьНаСервере();    
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура ОтклонитьНаСервере()
	МенеджерЗаписи = РегистрыСведений.ПодборКурьераКЗаказу.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Заказ = Объект.Ссылка;
	МенеджерЗаписи.Курьер = МенеджерКурьеров.ПолучитьКурьера();
	МенеджерЗаписи.ОтветКурьера = Перечисления.СтатусыПодбораКурьераКЗаказу.НеБеру;
	МенеджерЗаписи.Записать(); 	
КонецПроцедуры


&НаКлиенте
Процедура БеруВРаботу(Команда)
	ПринятьНаСервере(); 
	Закрыть();
КонецПроцедуры    

&НаСервере
Процедура ПринятьНаСервере()   
	Курьер = МенеджерКурьеров.ПолучитьКурьера();
	МенеджерЗаписи = РегистрыСведений.ПодборКурьераКЗаказу.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДата();
	МенеджерЗаписи.Заказ = Объект.Ссылка;
	МенеджерЗаписи.Курьер = Курьер;
	МенеджерЗаписи.ОтветКурьера = Перечисления.СтатусыПодбораКурьераКЗаказу.БеруВРаботу;
	МенеджерЗаписи.Записать(); 	
	Объект.Курьер = Курьер; 
	Объект.Состояние = Перечисления.СтадииДоставкиЗаказа.Начат;
	ЭтаФорма.Записать();
КонецПроцедуры


&НаКлиенте
Процедура ОтменитьДоставку(Команда)
	ОтменитьЗаказНаСервере();	
	Закрыть();
КонецПроцедуры


&НаКлиенте
Процедура ПодтвердитьДоставку(Команда)
	Если Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Начат") ИЛИ
		Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Задерживается") Тогда
		ЗабратьЗаказИзМагазинаНаСервере(); 	
		Иначе Если Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Доставляется") Тогда
			ДоставитьЗаказНаСервере();	
		КонецЕсли;
	КонецЕсли;
	Закрыть();
КонецПроцедуры   


&НаСервере
Процедура ДоставитьЗаказНаСервере()   
	Объект.Состояние = Перечисления.СтадииДоставкиЗаказа.Доставлен;
	ЭтаФорма.Записать();
КонецПроцедуры  

&НаСервере
Процедура ЗабратьЗаказИзМагазинаНаСервере()   
	Объект.Состояние = Перечисления.СтадииДоставкиЗаказа.Доставляется;
	ЭтаФорма.Записать();
КонецПроцедуры

&НаСервере
Процедура ОтменитьЗаказНаСервере()   
	Объект.Состояние = Перечисления.СтадииДоставкиЗаказа.Отменен;
	ЭтаФорма.Записать();
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЭтотЗаказВзят = ЭтотЗаказВзятКурьером();
	ЭтотЗаказЗавершен = ЭтотЗаказЗавершенКурьером();
	ЭтаФорма.Элементы.ГруппаКнопокДоставка.Видимость = ЭтотЗаказВзят И НЕ ЭтотЗаказЗавершен;
	ЭтаФорма.Элементы.ГруппаКнопокЗаявка.Видимость = НЕ ЭтотЗаказВзят;
	Если ЭтотЗаказНужноЗабратьИзМагазина() Тогда
		ЭтаФорма.Элементы.ПодтвердитьДоставку.Заголовок = "Забрал из магазина";			
	КонецЕсли;   
	Если ЭтотЗаказНужноДоставитьКлиенту() Тогда
		ЭтаФорма.Элементы.ПодтвердитьДоставку.Заголовок = "Доставил клиенту";		
	КонецЕсли;		        
КонецПроцедуры 

&НаКлиенте
Функция ЭтотЗаказВзятКурьером()
	Возврат НЕ Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.ПодборКурьера");
КонецФункции    

&НаКлиенте
Функция ЭтотЗаказЗавершенКурьером()
 	Возврат Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Доставлен") 
		ИЛИ Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Отменен");	
КонецФункции

&НаКлиенте
Функция ЭтотЗаказНужноЗабратьИзМагазина()
	Возврат Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Начат") ИЛИ
		Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Задерживается"); 	
КонецФункции

&НаКлиенте
Функция ЭтотЗаказНужноДоставитьКлиенту()
	Возврат Объект.Состояние = ПредопределенноеЗначение("Перечисление.СтадииДоставкиЗаказа.Доставляется");
КонецФункции


