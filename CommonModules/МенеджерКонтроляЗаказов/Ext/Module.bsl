
Процедура КонтрольОпаздывающихКурьеров() Экспорт  
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Заказы.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.Заказ КАК Заказы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодборКурьераКЗаказу.СрезПоследних КАК ПодборКурьераКЗаказуСрезПоследних
		|		ПО Заказы.Ссылка = ПодборКурьераКЗаказуСрезПоследних.Заказ
		|ГДЕ
		|	ПодборКурьераКЗаказуСрезПоследних.ОтветКурьера = ЗНАЧЕНИЕ(Перечисление.СтатусыПодбораКурьераКЗаказу.БеруВРаботу)
		|	И Заказы.Состояние = ЗНАЧЕНИЕ(Перечисление.СтадииДоставкиЗаказа.Начат)
		|	И РАЗНОСТЬДАТ(ПодборКурьераКЗаказуСрезПоследних.Период, &ТекущееВремя, МИНУТА) >= 60";
	Запрос.УстановитьПараметр("ТекущееВремя", ТекущаяДата());
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаказСсылка = ВыборкаДетальныеЗаписи.Ссылка;
		ЗаказОбъект = ЗаказСсылка.ПолучитьОбъект();
		ЗаказОбъект.Состояние = Перечисления.СтадииДоставкиЗаказа.Задерживается;
		ЗаказОбъект.Записать();
	КонецЦикла;
КонецПроцедуры 


Процедура ПодборКурьеровКЗаказу() Экспорт 
	ПодборДляНовыхЗаказов(); 
	ПодборДляНеотвеченныхЗаказов();
	ОтменитьНевзятыеЗаказы();	
КонецПроцедуры
 

Процедура ПодборДляНовыхЗаказов() 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Заказ.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.Заказ КАК Заказ
		|ГДЕ
		|	НЕ Заказ.Ссылка В
		|				(ВЫБРАТЬ
		|					ЭтапыПодбораКурьераКЗаказуСрезПоследних.Заказ КАК Заказ
		|				ИЗ
		|					РегистрСведений.ЭтапыПодбораКурьераКЗаказу.СрезПоследних КАК ЭтапыПодбораКурьераКЗаказуСрезПоследних)
		|	И Заказ.Состояние = ЗНАЧЕНИЕ(Перечисление.СтадииДоставкиЗаказа.ПодборКурьера)";	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаказСсылка = ВыборкаДетальныеЗаписи.Ссылка;
		ПредложитьЗаказОсновнымКурьерам(ЗаказСсылка);
	КонецЦикла;
КонецПроцедуры


Процедура ПодборДляНеотвеченныхЗаказов()	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапыПодбораКурьераКЗаказуСрезПоследних.Заказ КАК Заказ,
		|	ЭтапыПодбораКурьераКЗаказуСрезПоследних.Этап КАК Этап
		|ИЗ
		|	РегистрСведений.ЭтапыПодбораКурьераКЗаказу.СрезПоследних КАК ЭтапыПодбораКурьераКЗаказуСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Заказ КАК Заказы
		|		ПО (ЭтапыПодбораКурьераКЗаказуСрезПоследних.Заказ = Заказы.Ссылка)
		|ГДЕ
		|	РАЗНОСТЬДАТ(ЭтапыПодбораКурьераКЗаказуСрезПоследних.Период, &ТекущаяДата, МИНУТА) >= 30
		|	И Заказы.Состояние = ЗНАЧЕНИЕ(Перечисление.СтадииДоставкиЗаказа.ПодборКурьера)";
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаказСсылка = ВыборкаДетальныеЗаписи.Заказ;
		ЗаказМагазин = ЗаказСсылка.ПолучитьОбъект().Магазин;
		ЗаказЭтап = ВыборкаДетальныеЗаписи.Этап;
		Если ЗаказЭтап = Перечисления.СтадииПодбораКурьераКЗаказу.ОсновныеКурьеры Тогда
			ПредложитьЗаказГотовымКурьерамСПриоритетом(ЗаказСсылка, ЗаказМагазин);    
			Иначе Если ЗаказЭтап = Перечисления.СтадииПодбораКурьераКЗаказу.ГотовыеКурьерыСПриоритетом Тогда
				ПредложитьЗаказГотовымКурьерам(ЗаказСсылка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


Процедура ОтменитьНевзятыеЗаказы() 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапыПодбораКурьераКЗаказуСрезПоследних.Заказ КАК Заказ
		|ИЗ
		|	РегистрСведений.ЭтапыПодбораКурьераКЗаказу.СрезПоследних КАК ЭтапыПодбораКурьераКЗаказуСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Заказ КАК Заказы
		|		ПО ЭтапыПодбораКурьераКЗаказуСрезПоследних.Заказ = Заказы.Ссылка
		|ГДЕ
		|	РАЗНОСТЬДАТ(ЭтапыПодбораКурьераКЗаказуСрезПоследних.Период, &ТекущаяДата, МИНУТА) >= 10
		|	И Заказы.Состояние = ЗНАЧЕНИЕ(Перечисление.СтадииДоставкиЗаказа.ПодборКурьера)
		|	И ЭтапыПодбораКурьераКЗаказуСрезПоследних.Этап = ЗНАЧЕНИЕ(Перечисление.СтадииПодбораКурьераКЗаказу.ГотовыеКурьеры)";
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаказСсылка = ВыборкаДетальныеЗаписи.Заказ; 
		ЗаказОбъект = ЗаказСсылка.ПолучитьОбъект();
		ЗаказОбъект.Состояние = Перечисления.СтадииДоставкиЗаказа.Отменен;
		ЗаказОбъект.Записать();
	КонецЦикла;
КонецПроцедуры


Процедура ПредложитьЗаказОсновнымКурьерам(Заказ)  
	НачатьНовыйЭтапПодбораКурьераКЗаказу(Заказ, Перечисления.СтадииПодбораКурьераКЗаказу.ОсновныеКурьеры);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Курьеры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Курьеры КАК Курьеры
		|ГДЕ
		|	Курьеры.ВидКурьера = ЗНАЧЕНИЕ(Перечисление.ВидыКурьеров.Основной)";
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Курьер = ВыборкаДетальныеЗаписи.Ссылка;
		ПредложитьКурьеруЗаказ(Заказ, Курьер)
	КонецЦикла;	
КонецПроцедуры      

Процедура ПредложитьЗаказГотовымКурьерамСПриоритетом(Заказ, Магазин)
	НачатьНовыйЭтапПодбораКурьераКЗаказу(Заказ, Перечисления.СтадииПодбораКурьераКЗаказу.ГотовыеКурьерыСПриоритетом);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КурьерыПриоритетныеМагазины.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Курьеры.ПриоритетныеМагазины КАК КурьерыПриоритетныеМагазины
		|ГДЕ
		|	КурьерыПриоритетныеМагазины.Магазин = &Магазин
		|	И КурьерыПриоритетныеМагазины.Ссылка.ГотовностьКРаботе = ЗНАЧЕНИЕ(Перечисление.СтатусыГотовностиКурьеровКРаботе.Готов)";	
	Запрос.УстановитьПараметр("Магазин", Магазин);	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Курьер = ВыборкаДетальныеЗаписи.Ссылка;
		ПредложитьКурьеруЗаказ(Заказ, Курьер)
	КонецЦикла;
КонецПроцедуры  

Процедура ПредложитьЗаказГотовымКурьерам(Заказ)
	НачатьНовыйЭтапПодбораКурьераКЗаказу(Заказ, Перечисления.СтадииПодбораКурьераКЗаказу.ГотовыеКурьеры);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Курьеры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Курьеры КАК Курьеры
		|ГДЕ
		|	Курьеры.ГотовностьКРаботе = ЗНАЧЕНИЕ(Перечисление.СтатусыГотовностиКурьеровКРаботе.Готов)";	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Курьер = ВыборкаДетальныеЗаписи.Ссылка;
		ПредложитьКурьеруЗаказ(Заказ, Курьер);
	КонецЦикла;
КонецПроцедуры 


Процедура НачатьНовыйЭтапПодбораКурьераКЗаказу(Заказ, Этап)  
	МенеджерЗаписиЭтап = РегистрыСведений.ЭтапыПодбораКурьераКЗаказу.СоздатьМенеджерЗаписи();
	МенеджерЗаписиЭтап.Заказ = Заказ; 
	МенеджерЗаписиЭтап.Период = ТекущаяДата(); 
	МенеджерЗаписиЭтап.Этап = Этап;
	МенеджерЗаписиЭтап.Записать(); 	
КонецПроцедуры


Процедура ПредложитьКурьеруЗаказ(Заказ, Курьер)
	МенеджерЗаписиПредложениеКурьеру = РегистрыСведений.ПодборКурьераКЗаказу.СоздатьМенеджерЗаписи();
	МенеджерЗаписиПредложениеКурьеру.Заказ = Заказ;
	МенеджерЗаписиПредложениеКурьеру.Период = ТекущаяДата();
	МенеджерЗаписиПредложениеКурьеру.Курьер = Курьер; 
	МенеджерЗаписиПредложениеКурьеру.ОтветКурьера = Перечисления.СтатусыПодбораКурьераКЗаказу.НетОтвета;
	МенеджерЗаписиПредложениеКурьеру.Записать();	
КонецПроцедуры






